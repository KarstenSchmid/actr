plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.adarshr.test-logger' version '1.6.0'
    id "biz.aQute.bnd.builder" version "5.2.0"
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.zakgof'
archivesBaseName = 'actr'
version = '0.4.0-SNAPHOT'

ext {
   descr = 'Simple Java actor model implementation'
}

repositories {
    jcenter()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0-RC1'
}

sourceSets {
   test {
      java {
        srcDirs = ['src/example/java', 'src/test/java']
      }
   }
}

jar {
  bnd('-exportcontents': 'com.zakgof.actr')
}
 
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
     scm {
          connection 'scm:git:https://github.com/zakgof/actr.git'
          developerConnection 'scm:git:https://github.com/zakgof/actr.git'
          url 'https://github.com/zakgof/actr'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'zakgof'
            name 'Oleksandr Zakusylo'
            email 'zakgof@gmail.com'
          }
        }
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
  		    groupId group  		    
            artifactId archivesBaseName
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.descr)
                root.appendNode('name', project.archivesBaseName)
                root.appendNode('url', 'https://github.com/zakgof/actr.git')
                root.children().last() + pomConfig
            }
		}
	}
}

bintray {
    user = 'zakgof'
    key = project.hasProperty('bintrayKey') ? bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = 'zakgof'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/zakgof/actr.git'
        version {
            name = project.version
        }
    }
}

test {
    useJUnitPlatform()
}
