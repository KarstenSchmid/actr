plugins {
    id "java"
    id "maven"
    id "signing"
    id "maven-publish"
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'biz.aQute.bnd.builder' version '5.2.0'
    id 'org.sonarqube' version '3.1.1'
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.zakgof'
archivesBaseName = 'actr'
version = '0.4.2-SNAPHOT'

ext {
   descr = 'Simple Java actor model implementation'
}

repositories {
    jcenter()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0-RC1'
}

sourceSets {
   test {
      java {
        srcDirs = ['src/example/java', 'src/test/java']
      }
   }
}

jar {
  bnd('-exportcontents': 'com.zakgof.actr')
}
 
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

test {
    useJUnitPlatform()
}

artifacts {
    archives javadocJar, sourcesJar, testJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                artifactId 'actr'
                url 'https://github.com/zakgof/actr'
        
                name 'actr'
                description 'Simple Java actor model implementation'

                scm {
                    connection 'scm:git:https://github.com/zakgof/actr.git'
                    developerConnection 'scm:git:https://github.com/zakgof/actr.git'
                    url 'https://github.com/zakgof/actr/tree/master/actr'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'zakgof'
                        name 'Oleksandr Zakusylo'
                        email 'zakgof@gmail.com'
                    }
                }
            }
        }
    }
    
    sonarqube {
        properties {
            property "sonar.projectKey", "zakgof_actr"
            property "sonar.organization", "zakgof"
            property "sonar.host.url", "https://sonarcloud.io"
        }
    }
}
